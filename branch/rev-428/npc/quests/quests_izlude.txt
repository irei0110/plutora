//====================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 依斯魯得島 地區任務腳本 - 
//====================================
// 版本：1.1
//====================================
//= * 愛德嘉的協助
//====================================

izlude,182,186,2	script	愛德嘉#izlude	49,{
	if (MISC_QUEST & 16) {
		mes "[愛德嘉]";
		mes "這一次也是為了去艾爾貝塔又來找我的嗎?";
		mes "和上一次一樣只收250 金幣，把你送到艾爾貝塔";
		next;
		mes "[愛德嘉]";
		mes "要搭船嗎?";
		next;
		if (select("往艾爾貝塔移動!:結束") == 1) {
			if (Zeny < 250) {
				mes "[愛德嘉]";
				mes "錢不夠 !再去多借點錢來";
				close ;
			}
			set zeny,zeny-250;
			warp "alberta",195,164;
			end;
		}
		mes "[愛德嘉]";
		mes "好吧，再見了";
		close;
	}
	mes "[愛德嘉]";
	mes "我們依斯魯得島和艾爾貝塔港口之間用港口來做連繫，因為貿易交易頻繁，我都快要變成那邊的人了，";
	mes "特別是跟菲歷斯這位朋友交情還不錯呢，";
	mes "那傢伙特別小氣，想要凹他是不可能的";
	next;
	mes "[愛德嘉]";
	mes "可是他本性善良，喜歡幫助別人";
	mes "再加上他手氣很海派，稍微在他旁邊好好勸說的話，他可是會大力支援你的傢伙呢";
	next;
	mes "[愛德嘉]";
	mes "最近他在艾爾貝塔悄悄的，用傑勒比結晶在幫助人們呢";
	mes "若你的經濟情況不太好的話，可以試著去找他看看";
	next;
	if (select("請問怎麼到艾爾貝塔?:結束對話") == 1) {
		mes "[愛德嘉]";
		mes "嗯?";
		mes "用雙腳走也能努力的走到艾爾貝塔的，不過有錢的話，在那裡搭船就馬上到了";
		next;
		if (select("我知道了:我沒錢又不想用走的") == 1) {
			mes "[愛德嘉]";
			mes "你這人真無聊啊..";
			close;
		}
		mes "[愛德嘉]";
		mes "什麼?!";
		mes "沒有那麼多錢又不想用走的，又想要去艾爾貝塔? 真是的...";
		mes "好吧，我也是一位船長啊，多搭乘一位乘客又不是很難的事情，我特別只跟你收250 金幣，把你帶到艾爾貝塔";
		next;
		if(select("出發艾爾到貝塔!:結束")==1) {
			set MISC_QUEST,MISC_QUEST | 16;
			if (Zeny < 250) {
				mes "[愛德嘉]";
				mes "錢不夠 !再去多借點錢來";
				close;
			}
			set zeny,zeny-250;
			warp "alberta",195,164;
			end;
		}
		mes "[愛德嘉]";
		mes "好吧，一路小心";
		close;
	}
	mes "[愛德嘉]";
	mes "好吧，一路小心";
	close;
}