//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 吉芬地區 任務腳本 - 
//===================================
// 版本：1.3
//===================================


//===================================
//		鋼鐵面具
//===================================
geffen_in,144,166,4	script	鐵匠#01	63,{
	mes "[鐵匠]";
	mes "我，是頂尖的鐵匠師傅！";
	mes "你知道嗎？！";
	mes "我已經在灼熱的火焰般的環境工作了將近三十年了！";
	mes "我做了那麼多年鐵匠，經驗老到，你要聽聽嗎？";
	next;
	if (select("詢問秘訣:聽起來對我好像沒什麼幫助")==2) {
		mes "[鐵匠]";
		mes "算了..我不多浪費唇舌了，你走吧！";
		close;
	}
	mes "[鐵匠]";
	mes "我們鐵匠，利用你無法想像的高溫把鋼鐵熔化成許多金屬性";
	mes "因此我們不得不面臨著熾熱的溫度和紅通通的鐵水，一般人的皮膚是耐不住的..";
	mes "不過有了這個，現在就可以不用擔心了，那就是.. ^3355FF'鋼鐵面具'^000000！";
	next;
	mes "[鐵匠]";
	mes "在灼熱的環境中保護你的皮膚，讓皮膚不至於受到灼傷";
	mes "是任何人都極想要擁有的 ~ 夢幻道具";
	mes "如何？給我^2244FF' 50塊鋼鐵 '^000000和^4422FF2000 金幣^000000，我就做給你！";
	next;
	if (select("還不賴，我要做一個:好像沒有什麼用處的樣子")==2) {
		mes "[鐵匠]";
		mes "唉唷！";
		mes "居然不想要這麼迷人的面具，真是太沒有眼光了！";
		mes "年輕人，你不要仗著自己膚質良好就什麼都不顧了！";
		close;
	}
	if(countitem(999) < 50 || Zeny < 2000) {
		mes "[鐵匠]";
		mes "你看，你擁有的鋼鐵跟費用不足，我要怎麼幫你做呢?";
		mes "你知道什麼叫鍊金嗎?";
		mes "就是像你這類的人所需要的!";
		mes "為了明瞭鍊金的意義，你還是趕快去尋找鋼鐵和金幣吧!";
		close;
	}
	delitem 999,50;
	set Zeny,Zeny-2000;
	mes "[鐵匠]";
	mes "我喜歡你，城裡人！好，我馬上幫你做一個！";
	next;
	mes "[鐵匠]";
	mes "你是我見到的最爽快的人";
	mes "再見！~";
	getitem 2292,1;
	close;
}

//===================================
//		耳機
//===================================
geffen_in,30,71,4	script	鄭何意嘉	83,{
	mes "[鄭何意嘉]";
	mes "你要不要聽聽我爺爺說的故事呢?";
	next;
	switch (select("繼續對話:詢問材料:製作看看:取消")) {
	case 1:
		mes "[鄭何意嘉]";
		mes "前幾年剛過世的爺爺";
		mes "在我的生命中是個相當重要的人物";
		mes "比起我那忙碌的父親";
		mes "我可以感覺到爺爺依舊在我身邊守候著我";
		next;
		mes "[鄭何意嘉]";
		mes "爺爺的書房更是最令我感到有趣的地方";
		mes "就像是' 世外桃源 '一般";
		mes "每當我想念我爺爺的時候";
		mes "我就會到書房去找些書來看";
		next;
		mes "[鄭何意嘉]";
		mes "在那裡";
		mes "我可以透過書籍學習到許多的知識";
		mes "因為爺爺對於煉金術和音樂都相當的喜愛";
		mes "所以書房裡頭幾乎滿滿的都是相關的書籍";
		next;
		mes "[鄭何意嘉]";
		mes "直到有一天 ..";
		mes "我像往常一樣進到爺爺的書房裡";
		mes "在那裡我偶然發現了一張相當神奇的設計圖";
		next;
		mes "[鄭何意嘉]";
		mes "在設計圖中有一樣東西，如果將它像耳罩一樣戴上";
		mes "如果將它像耳罩一樣戴，透過一條細線的牽引";
		mes "將可以聽到像留聲機中一樣的聲音";
		mes "這就是設計圖的內容";
		next;
		mes "[鄭何意嘉]";
		mes "我 ..一定要完成爺爺的遺願";
		mes "做出這樣東西來";
		mes "所以我到處去將這設計圖給其他煉金者看";
		mes "請求他們幫忙，但是所有的人都說不知道這是什麼東西";
		next;
		mes "[鄭何意嘉]";
		mes "所以我才會想要親自動手來做做看";
		mes "經過我多年的苦心研究..";
		mes "我已經可以完全理解那圖上的結構了";
		mes "不過 .. 我還沒能收集到所需要的材料";
		next;
		mes "[鄭何意嘉]";
		mes "如果現在手邊有材料的話";
		mes "我就可以馬上依照設計圖動手製作了";
		mes "如果 .. 你感興趣的話";
		mes "是不是可以幫我找到所需要的材料呢?";
		next;
		mes "[鄭何意嘉]";
		mes "我只是想要完成爺爺的遺願";
		mes "做出設計圖上的東西而已";
		mes "完成後如果你要的話，給你也沒關係";
		close;
	case 2:
		mes "[鄭何意嘉]";
		mes "我需要^3355FF鋼鐵^000000 ^FF555540^000000 片";
		mes "^3355FF神之金屬^000000 ^FF55551^000000 個";
		mes "^3355FF酒精^000000 ^FF55551^000000 瓶";
		mes "^3355FF煤礦^000000 ^FF55551^000000 塊";
		mes "這些東西";
		close;
	case 3:
		if(countitem(999) < 40 || countitem(984) < 1 || countitem(970)<1 || countitem(1003) < 1) {
			mes "[鄭何意嘉]";
			mes "謝謝你的好意 .. 但是材料似乎不足";
			mes "請你備齊了再來吧！";
			close;
		}
		delitem 999,40;
		delitem 984,1;
		delitem 970,1;
		delitem 1003,1;
		mes "[鄭何意嘉]";
		mes "滴答滴答 ..";
		next;
		getitem 5001,1;
		mes "[鄭何意嘉]";
		mes "真是太感謝你了";
		mes "托你的福我才能完成爺爺的遺願";
		mes "如果爺爺能看到的話一定會很開心的";
		next;
		mes "[鄭何意嘉]";
		mes "誠如我之前說過的，完成品請你帶走吧！";
		mes "既然已經完成了，我就感到很高興了";
		close;
	case 4:
		mes "[鄭何意嘉]";
		mes "請慢走";
		close;
	}
}