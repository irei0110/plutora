//============================================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// -----------------------------------------
// 2008-02-13 01:00 黑色小貓 中文化生命倫理學習任務
// 刪除重複npc 翻譯任務對話
//============================================================
// 鍊金 - 屬性藥水製造
//============================================================
yuno_in04,33,108,5	script	皮斯瑞克	750,{
	mes "[皮斯瑞克]";
	if(BaseJob!=Job_Alchem) goto L_noalche;
	if(countitem(7434)) goto L_alreadyhave;
	if(JobLevel<40) goto L_nojob;

	mes "要我教你新技能";
	mes "但你得帶些東西給我";
	mes "5 黃色魔力礦石";
	mes "4 空藥瓶";
	mes "10 人魚之心";
	mes "10 蛾蝶蟲翅膀粉末";
	mes "20 噬人樹花";
	mes "1 旋轉眼鏡0洞";
	next;
	
	mes "[皮斯瑞克]";
	mes "讓我看看你帶來的物品..";
	next;
	
	if(countitem(715)<5 || countitem(1093)<4 || countitem(950)<10 || countitem(1057)<10 || countitem(1032)<20 || countitem(2243)<1) goto L_noitems;//Items: Yellow_Gemstone, Empty_Potion_Bottle, Heart_of_Mermaid, Moth_Dust, Maneater_Blossom, Geek_Glasses,
	delitem 715, 5;//Items: Yellow_Gemstone,
	delitem 1093, 4;//Items: Empty_Potion_Bottle,
	delitem 950, 10;//Items: Heart_of_Mermaid,
	delitem 1057, 10;//Items: Moth_Dust,
	delitem 1032, 20;//Items: Maneater_Blossom,
	delitem 2243, 1;//Items: Geek_Glasses,
	getitem 7434, 1;//Items: Elemental_Potion_Creation_Guide,

	mes "[皮斯瑞克]";
	mes "我想你都帶來了,";
	mes "我把我的手冊也給你.";
	close;
	
L_noitems:
	mes "[皮斯瑞克]";
	mes "你沒足夠的東西.";
	mes "等你有了再來找我";
	close;
	
L_noalche:
	mes "對不起，我的知識僅限於鍊金.";
	mes "很遺憾不能幫你..";
	emotion e_gasp;
	close;

L_alreadyhave:
	mes "我已經給你啦";
	mes "我的屬性藥水製造手冊...";
	close;

L_nojob:
	mes "當你成為一名鍊金時再來我找吧";
	close;
}


//============================================================
// 鍊金 - 生命倫理(人工生命體任務)
//============================================================

//=====================Requires Bug Testing=====================
lhz_in01,46,125,6	script	雷根貝勒 衛兵#99	867,{
	mes "[雷根貝勒 衛兵]";
	if(isequipped(2241) && isequipped(2243)){
		mes "......................";
		next;
		if(select("今天天氣不錯,是吧?:取消")==1){
			mes "[雷根貝勒 衛兵]";
			mes ".....";
			close2;
			warp "lhz_in01",282,172;
			end;
		}
	}
	mes "...";
	close;
}

lhz_in01,204,138,6	script	凱西貝安	750,{
	if(bioeth >= 2){
		mes "[凱西貝安]";
		mes "哈囉~";
		mes "你是來學習跟人工生命體相關的知識嗎?";
		next;
		mes "[凱西貝安]";
		mes "可是我實在不應該告訴你!";
		mes "你可以去找里希塔樂鎮的布陵澈";
		mes "應該會有些幫助!";
		if(bioeth == 2) set bioeth,3;
		close;
	}
	mes "[凱西貝安]";
	mes "哈囉~";
	mes "你一定是從鍊金術士研究所來的,對吧?!";
	mes "很高興見到你!";
	next;
	mes "[凱西貝安]";
	mes "我聽說那裡的鍊金術士都很厲害!";
	mes "我真的很希望有機會可以跟他們合作看看...";
	close;
}

lhz_in01,224,140,8	script	柯拉休斯	57,{
	if(MISC_QUEST&64){
		mes "[柯拉休斯]";
		mes "我已經把技能教給你了...";
		if(getskilllv(238) == 0 && BaseJob == Job_Alchem) skill 238,1,0;
		close;
	}
	switch(bioeth){
	case 4:
		mes "[柯拉休斯]";
		mes "...你真的很不受教!";
		mes "我不覺得我可以教你跟人工生命體相關的技能,";
		mes "你根本一點都不懂這方面的知識!";
		next;
		menu "...可是我已經學過了",-;
		mes "[柯拉休斯]";
		mes "喔? 真的嗎?";
		mes "那我要先做幾個測試,";
		mes "看看你到底懂多少?";
		next;
		mes "[柯拉休斯]";
		mes "第一個問題，";
		mes "什麼技能可以將生命體移動到眼睛看不見的地方做休息?";
		input @kelques$;
		next;
		mes "[柯拉休斯]";
		if(@kelques$ != "安息"){
			mes "哼! 我就知道你根本什麼都不懂!";
			close;
		}
		mes "答對了! 第二個問題.";
		mes "鍊金術士可以利用什麼東西招喚人工生命體?";
		input @kelques$;
		next;
		mes "[柯拉休斯]";
		if(@kelques$ != "艾普理歐"){
			mes "哼! 我就知道你根本什麼都不懂!";
			close;
		}
		mes "還不錯嘛~不過還有最後一個問題!";
		mes "什麼技能可以將戰鬥中死亡的生命體重新招喚出來?";
		input @kelques$;
		next;
		mes "[柯拉休斯]";
		if(@kelques$ != "復活生命體"){
			mes "哼! 我就知道你根本什麼都不懂!";
			close;
		}
		mes "經過這些討論之後,";
		mes "我決定我以後不會再教任何人有關人工生命體的技能了!";
		mes "這是我最後的決定!";
		set bioeth,5;
		close;
	case 5:
		mes "[柯拉休斯]";
		mes "經過這些討論之後,";
		mes "我決定我以後不會再教任何人有關人工生命體的技能了!";
		mes "這是我最後的決定!";
		close;
	case 6:
	//Unofficial text and story -.-;
		mes "[柯拉休斯]";
		mes "喔~ 又是你? 看來你已經決定了!";
		mes "嗯~ 大部分的鍊金術士都是不會放棄自己的夢想的!";
		next;
		menu "讓我們談點別的吧...",-;
		mes "[柯拉休斯]";
		mes "你見過我兒子? 他說了什麼?";
		next;
		mes "[柯拉休斯]";
		mes ".....";
		next;
		mes "[柯拉休斯]";
		mes "他希望我回家嗎? 嗯...";
		mes "我想我該給你一些回報,";
		mes "拿去吧! 這是你想知道的有關人工生命體的秘密!";
		if(BaseJob == Job_Alchem) skill 238,1,0;
		set MISC_QUEST,MISC_QUEST|64;
		set bioeth,0;
		close;
	default:
		break;
	}
	switch(@keltalk){
	case 1:
		mes "[柯拉休斯]";
		mes "誰決定什麼是對是錯?";
		mes "是什麼註定會被了解而哪些秘密將永遠無法預料會被人類所理解?";
		set @keltalk,2;
		close;
	case 2:
		mes "[柯拉休斯]";
		mes "難道我沒有跟你說不要再來了嗎?";
		mes "請你馬上離開!";
		mes "難道你沒有其他事情做了嗎?";
		mes "我不會告訴你任何關於人工生命體的知識的!";
		set @keltalk,3;
		set bioeth,1;
		close;
	case 3:
		mes "[柯拉休斯]";
		mes "難道我沒有跟你說不要再來了嗎?";
		mes "請你馬上離開!";
		mes "難道你沒有其他事情做了嗎?";
		mes "我不會告訴你任何關於人工生命體的知識的!";
		close;
	default:
		mes "[柯拉休斯]";
		mes "誰決定什麼是對是錯?";
		mes "是什麼註定會被了解而哪些秘密將永遠無法預料會被人類所理解?";
		if(BaseJob == Job_Alchem) set @keltalk,1;
		close;
	}
}

lhz_in01,225,122,4	script	蘇格拉撒德	754,{
	mes "[蘇格拉撒德]";
	mes "鍊金術是很令人驚奇的...";
	mes "它融合了許多科學以及各種不同的知識,";
	mes "但這並不是隨便什麼人都可以學會怎麼使用的...";
	next;
	mes "[蘇格拉撒德]";
	mes "想你想想看,雖然說是由人類所創造,";
	mes "但是生命體也是一種生命,";
	mes "將生命任意的安息又招喚出來...雖然是可行的,";
	mes "不過還不鼓勵這種不太好的行為...";
	if(bioeth == 1) set bioeth,2;
	close;
}
lhz_in02,269,273,4	script	布陵澈	709,{

	if(bioeth == 3){
		mes "[布陵澈]";
		mes "喔? 你是來打聽有關人工生命體的事情的嗎?";
		mes "嗯... 很抱歉... 我也幫不上什麼忙...";
		next;
		mes "[布陵澈]";
		mes "不過,我聽過有種技能叫做";
		mes "復活生命體";
		mes "聽起來在使用生命體上,這是個很重要的技能!";
		set bioeth,4;
		close;
	}
	mes "[布陵澈]";
	mes "這真是個很棒的旅館,對吧?";
	mes "在這裡我可以很放鬆,";
	mes "不用管那些煩人的事...";
	close;
}
lhz_in03,106,34,4	script	寇凌	706,{

	if(bioeth == 5){
		mes "[寇凌]";
		mes "你在研究所見過我爸爸?";
		mes "幫我轉告他我很想他!";
		set bioeth,6;
		close;
	}
	mes "[寇凌]";
	mes "爸爸在哪裡?";
	mes "我真的很希望爸爸可以常回家...";
	close;
}