//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 果汁製作 任務腳本 - 
//===================================
// 版本：1.8
//===================================

prt_in,47,173,2	script	莫利孫#juice	97,{
	mes "[小孩子 莫利孫]";
	if(MISC_QUEST&1 || @MORRISON > 10) {
		mes "哼... 滾開! 別管我!";
		mes "我會因為這些愚蠢的蔬菜水果而死, 你快滾一邊去吧!";
		emotion e_pif;
		close;
	}
	if(@MORRISON == 10) {
		mes "呃..我已經吃飽了.";
		mes "你不用再給我了. 在繼續吃下去的話我非撐死不可.";
		next;
		mes "[小孩子 莫利孫]";
		mes "那麼... 嗯... 這給你吧.";
		next;
		getitem 529,3;
		getitem 530,1;
		set @MORRISON,0;
		mes "[小孩子 莫利孫]";
		mes "這是我背著媽媽偷偷藏起來的糖果.";
		mes "如果再給我一些肉的話, 我願意拿糖果和你換." ;
		close;
	}
	mes "啊.. 啊啊啊啊!!! 不.. 不吃";
	mes "哼..不吃水果!!";
	emotion e_sob;
	next;
	if(select("對話:關閉")==2) {
		mes "[小孩子 莫利孫]";
		mes "啊啊啊啊!!受夠了!!已經不能在吃水果了!";
		mes "講實話,水果呢~先要撥皮還有很多的水份.吃的時候感覺不舒服";
		mes "而且會酸酸黏黏的..還吃不飽.啊啊啊啊!給我肉吧~";
		emotion e_sob;
		close;
	}
	if(countitem(517)) {
		mes "[小孩子 莫利孫]";
		mes "啊啊! 肉.肉.噢";
		mes "啊 肚 肚子餓了 真香  這  給我1塊肉行不行?";
		emotion e_ic;
		next;
		next;
		if(select("給他肉:不給他肉")==1) {
			if(countitem(517) == 0) {
				mes "哼... 滾開! 別管我!";
				mes "我會因為這些愚蠢的蔬菜水果而死, 你快滾一邊去吧!";
				emotion e_pif;
				close;
			}
			delitem 517,1;
			set @MORRISON,@MORRISON+1;
			mes "[小孩子 莫利孫]";
			mes "哇~!!! 真的是肉耶! 肉!";
			mes "咕嚕! 我口水都流下來了.";
			mes "真是太感謝你了. 我現在覺得快樂多了. 哈哈...";
			emotion e_awsm;
			close;
		}
		set @MORRISON,11;
		mes "[小孩子 莫利孫]";
		mes "哼...";
		mes "哼... 好... 很好, 非常好~!!! 自私的傢伙!";
		emotion e_bzz;
		close;
	}
	mes "[小孩子 莫利孫]";
	mes "啊啊啊啊!!受夠了!!已經不能在吃水果了!";
	mes "講實話,水果呢~先要撥皮還有很多的水份.吃的時候感覺不舒服";
	mes "而且會酸酸黏黏的..還吃不飽.啊啊啊啊!給我肉吧~";
	emotion e_wah;
	close;
}

prt_in,49,172,2	script	瑪麗亞稜#juice	53,{
	mes "[家庭主婦 瑪麗亞稜]";
	if(MISC_QUEST&1 || @MORRISON > 10) {
		mes "啊呦 沒有肉就不吃飯...要怎麼樣他才吃水果...";
		mes "舉個例子...抹成泥狀...";
		next;
		mes "[家庭主婦 瑪麗亞稜]";
		mes "啊!好像有一個城市會把水果抹成泥狀.";
		mes "那是斐揚? 夢羅克";
		emotion e_hmm;
		set @MORRISON,0;
		set MISC_QUEST,MISC_QUEST | 1;
		close;
	}
	if(@MORRISON) {
		set @MORRISON,0;
		mes "不!! 這..這到底";
		mes "世界上怎麼會發生這種事情!!你不要把奇怪的食物給我兒子吃!";
		mes "哎呀~~真煩!! 快滾出去!";
		emotion e_pif;
		close;
	}
	mes "莫利孫!!求你吃一點水果~!啊呦~!!活不下去了!";
	mes "這個壞習慣真像你爸...";
	emotion e_sob;
	next;
	if(select("交談:取消")==2) {
		mes "[家庭主婦 瑪麗亞稜]";
		mes "莫利孫!! 你再這樣我要發火了!";
		emotion e_bzz;
		close;
	}
	mes "[家庭主婦 瑪麗亞稜]";
	mes "嗯..這個小孩叫莫利孫是我的兒子";
	mes "這個傢伙最近不太敢吃水果.已經很久沒吃了 臉色都變不好一直想吃肉...";
	mes "哎..無論如何想個辦法讓他吃青菜和水果...";
	close;
}

payon_in03,188,146,5	script	馬雷克漢森#juice	86,{
	mes "[商人馬雷克漢森]";
	if(MISC_QUEST&1) {
		mes "歡迎光臨 您需要加工水果嗎?";
		next;
		switch (select("加工水果:對話:取消")) {
		case 1:
			mes "[商人馬雷克漢森]";
			mes "您需要什麼果汁?";
			next;
			switch (select("蘋果汁:香蕉汁:紅蘿蔔汁:葡萄汁:取消")) {
			case 1:
				set .@fruit,512;
				set .@juice,531;
				break;
			case 2:
				set .@fruit,513;
				set .@juice,532;
				break;
			case 3:
				set .@fruit,515;
				set .@juice,534;
				break;
			case 4:
				set .@fruit,514;
				set .@juice,533;
				break;
			case 5:
				mes "[商人馬雷克漢森]";
				mes "那麼...下次再來...";
				close;
			}
			if (countitem(.@fruit) == 0 || countitem(713) == 0 || Zeny < 3) {
				mes "[商人馬雷克漢森]";
				mes "噢噢!";
				mes "材料不足喔! 製作果汁至少要該水果一個、空瓶一個、還有3金幣喔!";
				next;
				mes "[商人馬雷克漢森]";
				mes "如果你把材料都帶齊的話，";
				mes "我會幫你製作果汁的!";
				close;
			}
			mes "[商人馬雷克漢森]";
			mes "需要多少?";
			next;
			switch (select("定購:把所有的全部給我組合:取消")) {
			case 1:
				while(1) {
					input .@input;
					if (.@input == 0) {
						mes "[商人馬雷克漢森]"																									;
						mes "停止交易了";
						close;
					}
					else if (.@input > 100) {
						mes "[商人馬雷克漢森]"																									;
						mes "一次最多只幫你製作100瓶果汁喔!";
						next;
					}
					else {
						break;
					}
					break;
				}
				set .@make,.@input;
				if(countitem(.@fruit) < .@make || countitem(713) <  .@make || Zeny < .@make * 3) {
					mes "[商人馬雷克漢森]";
					mes "這個,這個...材料不夠啊";
					emotion e_gasp;
					close;
				}
				break;
			case 2:
				set .@make,1000;
				if(Zeny/3 < .@make) set .@make, Zeny/3;
				if(countitem(.@fruit) < .@make) set .@make, countitem(.@fruit);
				if(countitem(713) < .@make) set .@make, countitem(713);
				if(.@make > 0) break;
				mes "[商人馬雷克漢森]";
				mes "噢...您的材料不夠呢!";
				emotion e_gasp;
				close;
			case 3:
				mes "[商人馬雷克漢森]";
				mes "噢! 那麼... 下次再見了.";
				close;
			}
			delitem .@fruit, .@make;
			delitem 713, .@make;
			set Zeny, Zeny - .@make * 3;
			getitem .@juice, .@make;
			mes "[商人馬雷克漢森]";
			mes "這是您要的果汁,味道一定很不錯.";
			mes "那麼...下次再來吧.";
			close;

		case 2:
			mes "[商人馬雷克漢森]";
			mes "# 果汁介紹 #";
			mes "蘋果果汁 -";
			mes "-蘋果 x1, 空瓶 x1, 3 金幣.";
			mes "";
			mes "香蕉果汁 -";
			mes "香蕉 x1, 空瓶 x1, 3 金幣.";
			mes "";
			mes "蘿蔔果汁 -";
			mes "蘿蔔 x1, 空瓶 x1, 3 金幣.";
			mes "";
			mes "葡萄果汁 -";
			mes "葡萄 x1, 空瓶 x1, 3 金幣.";
			close;
		case 3:
			mes "[商人馬雷克漢森]";
			mes "既然來找人,是不是應該說點什麼呢.";
			close;
		}
	}
	mes "請進，找我有什麼事情嗎?";
	next;
	if(select("對話.:取消.")==1) {
		mes "[商人馬雷克漢森]";
		mes "人類在還沒有具備豐富知識前，每個人都是吃水果過生活.";
		mes "水果是讓人類活著的原動力之一，有著大自然的祝福.";
		next;
		mes "[商人馬雷克漢森]";
		mes "現在生活條件好了.";
		mes "最近的年輕人反而不喜歡吃水果.";
		mes "所以想個辦法讓他們願意吃水果.";
		mes "結果是將水果打成果汁，讓他們方便食用而且味道更好";
		close;
	}
	mes "[商人馬雷克漢森]";
	mes "既然來找人，是不是應該說點什麼呢?";
	close;
}
