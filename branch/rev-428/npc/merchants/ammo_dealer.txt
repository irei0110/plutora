//======================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// --------------------------------------
// - 彈藥經銷商 腳本 -
//======================================
//= 版本: 1.3 [5511]
//======================================

que_ng,187,156,3	script	彈藥經銷商 托尼	86,{

	mes "[托尼]";
	if (BaseJob == Job_Gunslinger) {
		mes "我是彈牙托尼!";
		mes "如果你缺少子彈的話,";
		mes "來找我!!!";
		next;
		mes "[托尼]";
		mes "現在~! 朋友~夥計~ 老兄";
		mes "你需要些什麼東西?!";
		mes "挑選下~!";
		mes "然後付錢~!";
		next;
		switch(select("毒物子彈:火焰子彈:雷擊子彈:暗盲子彈:冰凍子彈:取消")) {
		case 1: callfunc "Bullet_Trade",937,10,13205; break;
		case 2: callfunc "Bullet_Trade",7097,2,13203; break;
		case 3: callfunc "Bullet_Trade",7053,3,13204; break;
		case 4: callfunc "Bullet_Trade",1024,5,13206; break;
		case 5: callfunc "Bullet_Trade",7054,2,13207; break;
		default:
			mes "[托尼]";
			mes "嗯~ 好吧~";
			mes "請以後再來找我~";
			mes "我, 彈牙托尼,";
			mes "會永遠在這裡的~!!!";
			close;
		}
	}
	mes "我是專門為槍手們提供貿易貨品的商人.";
	mes "我是彈牙托尼.";
	next;
	mes "[托尼]";
	mes "我想你應該不是一名槍手吧";
	mes "那你就隨便走走逛逛吧.";
	close;
}

function	script	Bullet_Trade	{
	mes "[托尼]";
	mes "輸入你想要購買的數量.";
	next;
	mes "[托尼]";
	mes "我交易30 發"+getitemname(getarg(2))+"需要";
	mes "1 塊強化武器金屬-級數一,";
	mes "1 塊強化武器金屬-級數二,";
	mes "和"+getarg(1)+" 個"+getitemname(getarg(0))+".";
	next;
	mes "[托尼]";
	mes "你能交易的最大數量為500.";
	mes "如果你想要取消, 輸入0.";
	next;
	input .@amount;
	mes "[托尼]";
	if (.@amount < 1 || .@amount > 500) {
		mes "無效的數量!";
		mes "重新輸入~!";
		close;
	} else if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {
			mes "我不能給你子彈因為你的背包已經裝滿了. 等你騰些空出來, 再來吧.";
			close;
		} else {
			mes "喔~ 很好!";
			mes "交易數額檢查完了!";
			mes "我將立刻和你交易.";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			getitem getarg(2),.@amount * 30;
			close;
		}
	} else {
		mes "嘖嘖嘖嘖~";
		mes "你沒有為你想要交易的物品準備足夠的材";
		mes "料.";
		mes "準備好了再來吧.";
		close;
	}
}
