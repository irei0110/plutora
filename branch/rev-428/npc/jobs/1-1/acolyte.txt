//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 服事 轉職腳本 - 
//===================================
// 版本：1.9
//===================================

prt_church,184,41,4	script	聖堂 神父#aco	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "[馬勒西斯 神父]";
				mes "經歷了許多的考驗";
				mes "有關你的活躍表現我也有聽聞";
				next;
				if (getskilllv(1) < 9) {
					mes "[馬勒西斯 神父]";
					mes "雖然我知道你想麻煩轉職為服事，";
					mes "但是也應該重新學習一下基本技能";
					next;
					mes "[馬勒西斯 神父]";
					mes "那下次再見吧";
					close;
				}
				mes "[馬勒西斯 神父]";
				mes "要時常懷著感謝的心侍奉憐愛我們的神，";
				mes "今後不管面臨什麼困難與磨練，";
				mes "也要以更虔誠的信仰來侍奉我們的神";
				next;
				skill 143,0,0;
				jobchange Job_Acolyte_High;
				skill 156,1,0;
				mes "[馬勒西斯 神父]";
				mes "最後，恭喜你能堅持到最後，克服困難與磨練，完成這一連串的修行";
				close;
			}
			else {
				mes "[馬勒西斯 神父]";
				mes "今後也請作為神的隨從而不斷努力吧";
				mes "您的未來將充滿幸福!";
				close;
			}
		}
		else {
			mes "[馬勒西斯 神父]";
			mes "度過了一段堅忍的歲月，重新誕生了";
			mes "你應該看到、聽到、經歷到很多東西了吧";
			next;
			mes "[馬勒西斯 神父]";
			mes "不過服事這條路，和你的經驗不太符合啊";
			mes "請你回憶，找到屬於你的路";
			close;
		}
	}
	mes "[馬勒西斯 神父]";
	mes "請問有什麼事情嗎?";
	next;
	switch(select("轉職成為服事:詢問有關轉職成為服事的條件:放棄")) {
	case 1:
		mes "[馬勒西斯 神父]";
		if (BaseJob == Job_Acolyte) {
			mes "開玩笑的吧?";
			mes "你不就已經是服事了嗎?";
			close;
		}
		else if (BaseJob != Job_Novice) {
			mes "你好像已經有其他職業了吧?";
			mes "要成為服事必須心無旁鶩";
			mes "專心的信仰我們的神";
			close;
		}
		if (job_acolyte_q == 0) {
			mes "你是真心想成為神的隨從嗎?";
			next;
			if (select("是的:還是算了") == 1) {
				mes "[馬勒西斯 神父]";
				mes "很好" +strcharinfo(0)+ " ...";
				mes "我接受你的轉職申請書";
				mes "你應該知道要成為神的隨從，";
				mes "必須要經過一連串嚴格的修練吧";
				next;
				mes "[馬勒西斯 神父]";
				mes "很好...你的任務是...";
				next;
				mes "[馬勒西斯 神父]";
				switch(rand(3)) {
				default:
					set job_acolyte_q,2;
					mes "請你去^000077普隆德拉東北方的教區^000000，";
					mes "尋找正在修行中的^000077魯帕卡法拉^000000神父";
					setquest 1001;
					break;
				case 1:
					set job_acolyte_q,3;
		          		mes "請你去^000077普隆德拉西南方的夢羅克^000000，";
					mes "尋找正在修行中的^000077瑪狄妲^000000修女";
					setquest 1002;
					break;
				case 2:
					set job_acolyte_q,4;
					mes "請你去^000077普隆德拉西北方的橋墩^000000，";
					mes "找正在修行中的^000077攸晤斯克^000000神父";
					setquest 1003;
					break;
				}
				next;
				mes "[馬勒西斯 神父]";
				mes "神父正等待著為了成為服事而虔心修行的人";
				mes "請你去告訴他你的意願吧!";
				close;
			}
			mes "[馬勒西斯 神父]";
			mes "是這樣子啊，如果你無心侍奉我們的神";
			mes "你也是無法成為'服事'的";
			close;
		}
		mes "這麼快就回來了?";
		mes "忘記你的任務了嗎...?";
		next;
		mes "[馬勒西斯 神父]";
		if (getskilllv(1) < 9) {
			mes "什麼?!到現在還沒學滿'基本技能'!";
			mes "必須先學好'基本技能'才能轉職啊!";
			close;
		}
		if (job_acolyte_q < 5) {
			mes "修行完成名單上還沒有你的名字...";
			next;
			switch(job_acolyte_q) {
			case 2:
				mes "[馬勒西斯 神父]";
				mes "請你去^000077普隆德拉東北方的教區^000000，";
				mes "尋找正在修行中的^000077魯帕卡法拉^000000神父";
				break;
			case 3:
				mes "[馬勒西斯 神父]";
	          		mes "請你去^000077普隆德拉西南方的夢羅克^000000，";
				mes "尋找正在修行中的^000077瑪狄妲^000000修女";
				break;
			case 4:
				mes "[馬勒西斯 神父]";
				mes "請你去^000077普隆德拉西北方的橋墩^000000，";
				mes "找正在修行中的^000077攸晤斯克^000000神父";
				break;
			}
			next;
			mes "[馬勒西斯 神父]";
			mes "神父正等待著為了成為服事而虔心修行的人";
			mes "請你去告訴他你的意願吧!";
			close;
		}
		mes "很高興又見到你了";
		mes "現在修行完成名單上已經加入你的名字了";
		mes "我已經承認了你的修行，同意你轉職為服事了";
		next;
		skill 143,0,0;
		callfunc "Job_Change",Job_Acolyte;
		callfunc "F_ClearJobVar";
		if(checkquest(1001) != -1) {
			completequest 1001;
		}
		else if(checkquest(1002) != -1) {
			completequest 1002;
		}
		else {
			completequest 1003;
		}
		mes "[馬勒西斯 神父]";
		mes "要時常懷著感謝的心侍奉憐愛我們的神，";
		mes "今後不管面臨什麼困難與磨練，";
		mes "也要以更虔誠的信仰來侍奉我們的神";
		next;
		mes "[馬勒西斯 神父]";
		mes "最後，恭喜你能堅持到最後，克服困難與磨練，完成這一連串的修行";
		close;
	case 2:
		mes "[馬勒西斯 神父]";
		mes "你想要轉職成為服事嗎?";
		mes "那麼，你必須要通過以下這２項修練才行";
		mes "請認真克服困難";
		next;
		mes "[馬勒西斯 神父]";
		mes "第一，初學者的基本技能等級要到達９級";
		mes "這是要在這世界上生存下去的最基本修行，請你好好的修練";
		next;
		mes "[馬勒西斯 神父]";
		mes "第二，必須要克服所有的考驗再回到這來";
		mes "只要在服事轉職申請書上，寫上你所選擇的修行地就可以了";
		next;
		mes "[馬勒西斯 神父]";
		mes "當你完成這兩項修練後請再回到這個地方來，";
		mes "那麼你就能夠成為服事這個神聖職業了";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	修行中的神父#aco	89,{
	mes "[神父 魯帕卡法拉]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "當你快要到達的時候，";
			mes "我就已經跟聖堂聯絡好了";
			mes "你回去的路上要小心點";
			next;
			mes "[神父 魯帕卡法拉]";
			mes "不曉得信鴿是否已經到聖堂了...";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "嗯，你就是那位想成為服事的人嗎?";
				mes "我已經接到聖堂那邊有關你的消息了";
				mes "您的名字是... " +strcharinfo(0)+ "...";
				mes "對!就是你";
				next;
				mes "[神父 魯帕卡法拉]";
				mes "你應該已經從馬勒西斯神父那了解有關服事的事情了吧!";
				mes "所以我就不再多說了";
				mes "哈哈哈...";
				next;
				mes "[神父 魯帕卡法拉]";
				mes "如果想要完成修練的話，請你一定要記得：神會在你身旁";
				mes "因為神也不是沉默寡言的類型哈哈哈哈...";
				next;
				mes "[神父 魯帕卡法拉]";
				mes "不管怎樣，我會寄出你來過這的確認書，你可以往教堂出發了";
				next;
				mes "[神父 魯帕卡法拉]";
				mes "一路上小心";
				close2;
				savepoint "prt_fild03",361,255;
				set job_acolyte_q,6;
				end;
			}
			else {
				mes "嗯?";
				mes "你就是那位想成為服事的人嗎?";
				mes "我已經接到聖堂那邊有關你的消息了";
				mes "你的名字是... " +strcharinfo(0)+ "...";
				mes "可是名單上面並沒有你的名字...";
				next;
				mes "[神父 魯帕卡法拉]";
				mes "....請你回聖堂去確認一下好嗎...?";
				close;
			}
		}
		else {
			mes "嗯?為什麼你會到這樣的地方來?";
			mes "我不知道你是服事修行者還是旅行者，";
			mes "對於你來說這地方太危險了，";
			mes "還是早點回去吧";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "為什麼又回到這裡了呢?";
			mes "趕快回去繼續你的修行";
			close;
		}
		else {
			mes "哎呀?一個人到這種地方修行嗎?還是旅行者?";
			mes "不要以為這裡的怪物像樣子看來那麼好應付的";
			close;
		}
	}
}

moc_fild07,41,355,4	script	修行中的修女#2aco	95,{
	mes "[修女 瑪狄妲]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "我會派信鴿將確認書寄出去，";
			mes "你快要到達聖堂的時候，";
			mes "負責的神父應該已經收到通知了";
			mes "你走上成為服事的路，神會為你祈禱的";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "啊，你是服事修行者啊";
				mes "很高興你能平安的到達這裡";
				next;
				set CHANGE_AL,5;
				savepoint "moc_fild07",36,355;
				mes "[修女 瑪狄妲]";
				mes "名字是什麼呢?讓我看看...";
				mes "啊，在這裡!";
				mes strcharinfo(0)+ "，名單上有你的名字";
				mes "既然平安來到這，我會幫你寄出確認書的";
				mes "請你回聖堂去跟負責的神父確認一下吧";
				close2;
				savepoint "moc_fild07",35,355;
				set job_acolyte_q,7;
				end;
			}
			else {
				mes "你是服事修行者啊";
				mes "很高興你能平安的到達這裡";
				mes "名字是什麼呢?讓我看看...";
				mes "嗯?沒有看到有你的名字...";
				next;
				mes "[修女 瑪狄妲]";
				mes "你最好回聖堂去跟神父再確認一下比較好";
				close;
			}
		}
		else {
			mes "...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		mes "願神保佑你...";
		close;
	}
}

prt_fild00,208,218,6	script	修行中的神父#3aco	98,{
	mes "[神父 攸晤斯克]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "你怎麼又來了?你不用再來找我了";
			mes "你應該要回教會去!";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "你是服事修行者嗎?";
				next;
				mes "[神父 攸晤斯克]";
				mes "很高興你能平安的到達這裡";
				mes "名字是 " +strcharinfo(0)+ " 吧?";
				next;
				mes "[神父 攸晤斯克]";
				mes "名字是 " +strcharinfo(0)+ "";
				mes "我會將你已經來過這裡的確認書寄出";
				mes "請回聖堂去跟神父確認一下吧";
				close2;
				savepoint "prt_fild00",206,230;
				set job_acolyte_q,8;
				end;
			}
			else {
				mes "你好像找錯地方了";
				mes "...請你再回聖堂去跟神父確認一下吧";
				close;
			}
		}
		else {
			mes "是初學者啊，";
			mes "有話想要對我說嗎?";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		mes "....";
		close;
	}
}

