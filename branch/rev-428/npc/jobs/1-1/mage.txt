//==================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ---------------------------------
// - 魔法師 轉職腳本 - 
//===================================
// 版本：1.7a
//===================================

geffen_in,164,124,4	script	魔法工會服務人員#gef1	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[魔法工會服務人員]";
				mes "哇，好久沒看到你了";
				mes "我還以為你已經掛了呢";
				next;
				mes "[魔法工會服務人員]";
				mes "你應該是獲得重生了吧，";
				mes "不管怎樣，祝你一切平安";
				next;
				if (getskilllv(1) < 9) {
					mes "[魔法工會服務人員]";
					mes "....都準備好了嗎?";
					mes "....什麼還沒準備好啊，'基本技能'等級還不夠";
					mes "等提高了'基本技能'等級後再來吧";
					close;
				}
				skill 143,0,0;
				jobchange Job_Mage_High;
				skill 157,1,0;
				mes "[魔法工會服務人員]";
				mes "恭喜您轉職成功!";
				mes "這輩子也請為了魔法工會的復興努力奮鬥吧!";
				close;
			}
			else {
				mes "[魔法工會服務人員]";
				mes ".....";
				close;
			}
		}
		else {
			mes "[魔法工會服務人員]";
			mes "你對魔法有興趣嗎? 就算是也辦法";
			mes "畢竟你不屬於這裡，";
			mes "你應該到你該去的地方";
			close;
		}
	}
	mes "[魔法工會服務人員]";
	mes "你好，有什麼事情嗎?";
	next;
	switch(select("轉職成為魔法師:詢問有關轉職成為魔法師所需要的條件:取消")) {
	case 1:
		mes "[魔法工會服務人員]";
		if (BaseJob == Job_Mage) {
			mes "啊...你不已經是魔法師了嗎?";
			mes "難道你不知道嗎?";
			mes "難不成要能空手變出火焰時，你才知道自己是魔法師嗎?";
			mes "多實踐才是王道啊，你說是不是?";
			close;
		}
		if (BaseJob != Job_Novice) {
			mes "喲...你已經不能轉職為魔法師了";
			mes "你不是已經有其他職業了嗎?";
			close;
		}
		if (job_magician_q == 0) {
			mes "你想成為魔法師?";
			if (sex)
				mes "像你這麼可愛的小帥哥";
			else {
				mes "像你這麼可愛的正妹";
			}
			mes "如果能成為魔法工會的一員，一定會很受歡迎的~";
			next;
			mes "[魔法工會服務人員]";
			mes "說歸說，你想轉職成為魔法師嗎?";
			mes "那麼，麻煩你在魔法師轉職申請書上簽個名";
			next;
			if (select("申請轉職成為魔法師:取消") == 1) {
				mes "[魔法工會服務人員]";
				mes "對，在那裡簽名....";
				mes "噢，寫得真好看—";
				mes "嗯，好了，好了";
				mes "名字叫...." +strcharinfo(0)+ "吧";
				next;
				mes "[魔法工會服務人員]";
				mes "你的轉職考試項目是....";
				mes "做出魔法師轉職考試要求的";
				switch(rand(3)) {
				case 1:
					mes "^3355FF混合液１號^000000";
					set job_magician_q,1;
					setquest 1005;
					break;
				case 2:
					mes "^3355FF混合液２號^000000";
					set job_magician_q,2;
					setquest 1006;
					break;
				case 3:
					mes "^3355FF混合液３號^000000";
					set job_magician_q,3;
					setquest 1007;
					break;
				default:
					mes "^3355FF混合液４號^000000";
					set job_magician_q,4;
					setquest 1008;
				}
				next;
				getitem 1092,1; //空試管
				mes "[魔法工會服務人員]";
				mes "做混合液所需的所有材料，";
				mes "工會裡的書籍上都有寫，請你自己去找，";
				mes "書放在你右邊中央的書櫃";
				next;
				mes "[魔法工會服務人員]";
				mes "你看得到中間的那個機器吧?";
				mes "要調製混合液的話，請利用那個機器";
				mes "那麼，加油吧~";
				close;
			}
			mes "[魔法工會服務人員]";
			mes "什麼啊!你不是說過想當成魔法師?";
			mes "呼呼!";
			close;
		}
		mes "....都準備好了嗎?";
		if (getskilllv(1) < 9) {
			mes "....什麼還沒準備好啊，";
			mes "初學者的技能等級還不夠";
			mes "等提高了初學者技能等級後再來吧";
			close;
		}
		mes strcharinfo(0)+ "的轉職考試項目是....";
		mes "做出魔法師轉職考試要求的";
		if (job_magician_q == 1)
			mes "混合液１號";
		else if (job_magician_q == 2)
			mes "混合液２號";
		else if (job_magician_q == 3)
			mes "混合液３號";
		else
			mes "混合液４號";
		next;
		mes "[魔法工會服務人員]";
		mes "那麼讓我檢查一下";
		mes "你製作的混合液吧";
		next;
		mes "[魔法工會服務人員]";
		if (countitem(1071) == 0 && countitem(1085) == 0 && countitem(1086) == 0 && countitem(1087) == 0 && countitem(1090) == 0) {
			mes "之前的混合液溶液的試管在哪裡呢....?";
			mes "這樣無法評價你是否能成為魔法師的";
			close;
		}
		else {
			if ((job_magician_q == 1 && countitem(1071) == 0) || (job_magician_q == 2 && countitem(1085) == 0) || (job_magician_q == 3 && countitem(1086) == 0) || (job_magician_q == 4 && countitem(1087) == 0)) {
				mes "....是不是搞錯了";
				mes "這不是你轉職考試所要求的";
				mes "混合液";
				mes "請你重新製作了....";
				next;
				mes "[魔法工會服務人員]";
				mes "你的轉職考試項目是....";
				mes "做出魔法師轉職考試要求的";
				if (job_magician_q == 1)
					mes "混合液１號";
				else if (job_magician_q == 2)
					mes "混合液２號";
				else if (job_magician_q == 3)
					mes "混合液３號";
				else
					mes "混合液４號";

				if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
				else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
				else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
				else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
				else delitem 1090,1; //Mage_Test_Etc
				close;
			}
		}
		if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
		else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
		else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
		else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
		else delitem 1090,1; //Mage_Test_Etc
		mes "....嗯，相信你已經盡力了";
		mes "雖然做的不是很完美，";
		mes "但是這個程度應該可以了";
		mes "........";
		next;
		mes "[魔法工會服務人員]";
		mes "很好!你已經通過魔法師轉職考試，";
		mes "我就依你的意願將你轉職成為魔法師吧!";
		mes "你成為了我們的兄弟姊妹~";
		mes "你就是魔法師!";
		mes "恭喜你成為我們魔法工會的一員!";
		next;
		callfunc "Job_Change",Job_Mage;
		callfunc "F_ClearJobVar";
		set Zeny,Zeny+50;
		if(checkquest(1005) != -1) {
			completequest 1005;
		}
		else if(checkquest(1006) != -1) {
			completequest 1006;
		}
		else if(checkquest(1007) != -1) {
			completequest 1007;
		}
		else {
			completequest 1008;
		}
		mes "[魔法工會服務人員]";
		mes "歡迎你來到魔法工會!";
		mes "哎喲，只是打個招呼嘛，";
		mes "你也不用臉紅的—";
		mes "怎麼不說句話呢—";
		next;
		mes "[魔法工會服務人員]";
		mes "總之，恭喜你成為我們魔法工會的一員!";
		mes "以後還請多多指教~";
		close;
	case 2:
		mes "[魔法工會服務人員]";
		mes "你想成為魔法師?";
		mes "如果你能成為魔法工會的一員，";
		mes "一定會很受歡迎的~";
		next;
		mes "[魔法工會服務人員]";
		mes "首先，初學者的技能要達到９以上";
		mes "其次，必須要做出魔法師轉職要求的混合液";
		next;
		mes "[魔法工會服務人員]";
		mes "你要做什麼樣的混合液，";
		mes "要等你填寫完魔法師轉職申請書後，";
		mes "我才能告訴你";
		next;
		mes "[魔法工會服務人員]";
		mes "等你認為自己已經符合這兩項轉職條件後，";
		mes "再來向我詢問其他的事項吧";
		close;
	case 3:
		mes "[魔法工會服務人員]";
		mes "應該沒事了吧，恩?";
		close;
	}
}

geffen_in,164,112,4	script	物品 調和機	111,{
	mes "[物品 調和機]";
	mes "魔法師轉職考試專用物品調和機";
	mes "本器材是吉芬魔法協會的所有物";
	mes "請善加利用";
	next;
	if (select("使用物品調和機:取消") == 1) {
		mes "[物品 調和機]";
		mes "請選擇要調和的水溶液";
		next;
		switch(select("斐揚水溶液:夢羅克水溶液:不使用水溶液")) {
		case 1:
			if (countitem(1089) == 0) {
				mes "[物品 調和機]";
				mes "錯誤，您身上沒有該物品";
				close;
			}
			set .@mixitem2,1;
			break;
		case 2:
			if (countitem(1088) == 0) {
				mes "[物品 調和機]";
				mes "錯誤，您身上沒有該水溶液";
				close;
			}
			set .@mixitem2,2;
			break;
		case 3:
			set .@mixitem2,0;
			break;
		}
		while (1) {
			if (.@progress == 2) {
				mes "[物品 調和機]";
				if (.@mixitem1_1 != 0) mes "傑勒比結晶: " + .@mixitem1_1 + "個";
				if (.@mixitem1_2 != 0) mes "毛: " + .@mixitem1_2 + "個";
				if (.@mixitem1_3 != 0) mes "牛奶: " + .@mixitem1_3 + "個";
				if (.@mixitem2 == 0) mes "水溶液: 無";
				if (.@mixitem2 == 1) mes "水溶液: 斐揚水溶液.";
				if (.@mixitem2 == 2) mes "水溶液: 夢羅克水溶液";
				next;
				mes "[物品 調和機]";
				mes "現在要調和物品嗎?";
				mes "還是要繼續設定物品呢?";
				next;
				switch(select("調和物品:繼續設定物品:重新設定")) {
				case 1:
					mes "[物品 調和機]";
					mes "請你將設定好的物品放入調和桶中";
					mes "一旦將物品放進去後，就不能再加減數量";
					next;
					mes "[物品 調和機]";
					mes "請再仔細確認，";
					mes "確認完成後請按'調和開始'按鈕，";
					mes "如果要停止請按'結束'按鈕";
					next;
					if (select("調和開始:結束") == 1) {
						mes "[物品 調和機]";
						mes "將設定好的物品放入調和機";
						mes "請稍候";
						next;
						mes "[物品 調和機]";
						if (countitem(909) < .@mixitem1_1) {
							mes "'傑勒比結晶'的數量錯誤，請重新確認";
							close;
						}
						else if (countitem(914) < .@mixitem1_2) {
							mes "'毛'的數量錯誤，請重新確認";
							close;
						}
						else if (countitem(519) < .@mixitem1_3) {
							mes "'牛奶'的數量錯誤，請重新確認";
							close;
						}
						else if (.@mixitem2 == 1 || .@mixitem2 == 2) {
							if (countitem(1089) == 0 && countitem(1088) == 0) {
								mes "無法找到水溶液，請重新確認";
								close;
							}
						}
						if (.@mixitem1_1 != 0) delitem 909,.@mixitem1_1; //Jellopy
						if (.@mixitem1_2 != 0) delitem 914,.@mixitem1_2; //Fluff
						if (.@mixitem1_3 != 0) delitem 519,.@mixitem1_3; //Milk
						if (.@mixitem2 == 1) delitem 1089,1; //Payon_Potion
						if (.@mixitem2 == 2) delitem 1088,1; //Morocc_Potion
						mes "將設定好的物品放入調和箱子完畢";
						mes "關閉調和箱子";
						set .@progress,3;
						next;
					}
					break;
				case 2:
					set .@continue,0;
					next;
					break;
				case 3:
					set .@mixitem1_1,0;
					set .@mixitem1_2,0;
					set .@mixitem1_3,0;
					set .@progress,0;
					set .@continue,0;
					mes "[物品 調和機]";
					mes "Reset Complete.";
					mes "Initiate again?";
					next;
					if (select("Yes.:No.") == 1)
						break;
					mes "[物品 調和機]";
					mes "謝謝你的使用";
					close;
				}
				if (.@progress == 3) break;
			}
			else if (.@progress == 1) {
				mes "[物品 調和機]";
				mes "找不到東西";
				next;
			}
			mes "[物品 調和機]";
			mes "請選擇要調和的物品：";
			while(1) {
				switch(select("傑勒比結晶:毛:牛奶:查看所設定的物品明細")) {
				case 1:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[物品 調和機]";
							mes "物品數量輸入超出範圍";
							mes "請設置10000個以下的非負值";
							next;
						}
						else {
							if (countitem(909) > 0) set .@mixitem1_1,.@mixitem1_1 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 2:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[物品 調和機]";
							mes "物品數量輸入超出範圍";
							mes "請設置10000個以下的非負值";
							next;
						}
						else {
							if (countitem(914) > 0) set .@mixitem1_2,.@mixitem1_2 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 3:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							mes "[物品 調和機]";
							mes "物品數量輸入超出範圍";
							mes "請設置10000個以下的非負值";
							next;
						}
						else {
						if (countitem(519) > 0) set .@mixitem1_3,.@mixitem1_3 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;

				case 4:
					if (.@progress != 2)
						set .@progress,1;
					set .@continue,1;
					next;
				}
				if (.@continue) break;
			}
		}
		mes "[物品 調和機]";
		mes "請將調和的魔法粉末號碼寫下來";
		next;
		while(1) {
			input .@input;
			if (.@input < 1000 || .@input > 9999) {
				mes "[物品 調和機]";
				if (.@input == 0) {
					mes "確定不使用魔法粉末嗎?";
					next;
					if (select("是的:不") == 1) {
						break;
					}
				}
				else {
					mes "超出輸入範圍";
					mes "請重新輸入";
					next;
				}
			}
			else {
				mes "[物品 調和機]";
				mes "號碼 #" + .@input + " 魔法粉末";
				mes "對嗎?";
				next;
				if (select("是的:不是") == 1) {
					if (.@input == 8472)
						set .@magic_powder,1;
					else if (.@input == 3735)
						set .@magic_powder,2;
					else if (.@input == 2750)
						set .@magic_powder,3;
					else if (.@input == 5429)
						set .@magic_powder,4;
					else
						set .@magic_powder,5;
				}
				break;
			}
		}
		mes "[物品 調和機]";
		mes "最後，請選擇催化石";
		next;
		switch(select("黃色魔力寶石:紅色魔力寶石:藍色魔力寶石:透明的寶石:跳過")) {
		case 1: set .@mixitem3,1; break;
		case 2: set .@mixitem3,2; break;
		case 3: set .@mixitem3,3; break;
		case 4: set .@mixitem3,4; break;
		case 5: break;
		}
		mes "[物品 調和機]";
		mes "已經準備好了，現在可以開始調和了";
		mes "請稍候";
		next;
		mes "[物品 調和機]";
		mes "- 每日一句 -";
		switch(rand(1,5)) {
		case 1:
			mes "「被最信任的人背叛」";
			mes "意思就是說太過容易輕信別人，受傷的往往會是自己";
			break;
		case 2:
			mes "「冤家路窄」";
			mes "意思就是說與人結怨的話無可避免的還是得跟那個人碰面，最後容易為自己招來橫禍";
			break;
		case 3:
			mes "「遠親不如近鄰」";
			mes "意思就是說當緊急事情發生的時候在遠處的親人朋友幫不上你的忙，倒是街頭巷尾的人可以給你一些幫助";
			break;
		case 4:
			mes "意思是說常說謊話的話，到最後可能會落得大家都不相信你的地步";
			break;
		default:
			mes "意思就是說你要先主動對別人好，別人才會也對你好";
		}
		next;
		if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 1 && .@mixitem2 == 1 && .@mixitem3 == 1 && .@magic_powder == 1) {
			mes "[物品 調和機]";
			mes "魔法師轉職考試要求的混合液1號";
			getitem 1071,1; //Mage_Test_1
			next;
		}
		else if (.@mixitem1_1 == 3 && .@mixitem1_2 == 1 && .@mixitem1_3 == 1 && .@mixitem2 == 0 && .@mixitem3 == 2 && .@magic_powder == 2) {
			mes "[物品 調和機]";
			mes "魔法師轉職考試要求的混合液2號";
			getitem 1085,1; //Mage_Test_2
			next;
		}
		else if (.@mixitem1_1 == 6 && .@mixitem1_2 == 1 && .@mixitem1_3 == 0 && .@mixitem2 == 1 && .@mixitem3 == 3 && .@magic_powder == 3) {
			mes "[物品 調和機]";
			mes "魔法師轉職考試要求的混合液3號";
			getitem 1086,1; //Mage_Test_3
			next;
		}
		else if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 0 && .@mixitem2 == 2 && .@mixitem3 == 4 && .@magic_powder == 4) {
			mes "[物品 調和機]";
			mes "魔法師轉職考試要求的混合液4號";
			getitem 1087,1; //Mage_Test_4
			next;
		}
		else {
			mes "[物品 調和機]";
			mes "製作混合液的過程中發生了某些問題";
			getitem 1090,1; //Mage_Test_Etc
			next;
		}
		mes "[物品 調和機]";
		mes "調和完成了";
		mes "調和機作業終止";
		mes "感謝你的使用";
	}
	close;
}

pay_arche,122,100,0	script	魔法工會服務人員#pay1	88,{
	mes "[魔法工會服務人員]";
	mes "要購買水溶液嗎?";
	mes "那麼需要50 金幣和至少１個空試管";
	next;
	if (select("購買水溶液:放棄") == 1) {
		mes "[魔法工會服務人員]";
		if (Zeny < 50) {
			mes "很可惜，您的金錢不足";
			close;
		}
		if (countitem(1092) == 0) {
			mes "空著手的話物品要怎麼攜帶呢?";
			mes "帶一個空試管裝來吧";
			close;
		}
		delitem 1092,1; //空試管
		set zeny,zeny-50;
		getitem 1089,1; //Payon_Potion
	}
	close;
}

moc_ruins,91,150,0	script	魔法工會服務人員#moc1	93,{
	mes "[魔法工會服務人員]";
	mes "要購買水溶液嗎?";
	mes "那麼需要50 金幣和至少１個空試管";
	next;
	if (select("購買水溶液:放棄") == 1) {
		mes "[魔法工會服務人員]";
		if (Zeny < 50) {
			mes "很可惜，您的金錢不足";
			close;
		}
		if (countitem(1092) == 0) {
			mes "空著手的話物品要怎麼攜帶呢?";
			mes "帶一個空試管裝來吧";
			close;
		}
		delitem 1092,1; //空試管
		set zeny,zeny-50;
		getitem 1088,1; //Morocc_Potion
	}
	close;
}

geffen_in,177,112,0	script	書架#geffen	111,{
	mes "[物品調和書籍]";
	mes "本書是吉芬魔法協會的所有物，";
	mes "請善加利用";
	next;
	switch (select("混合液1號:混合液2號:混合液3號:混合液4號:停止")) {
	case 1:
		mes "[魔法師轉職考試要求的混合液1號]";
		mes "* 材料明細 *";
		mes " - 傑勒比結晶 2 個";
		mes " - 毛 3 個";
		mes " - 牛奶 1 個";
		next;
		mes "[魔法師轉職考試要求的混合液1號]";
		mes "* 溶劑 *";
		mes "斐揚水溶液";
		mes "所在地：斐揚弓箭手村莊的小泉";
		next;
		mes "[魔法師轉職考試要求的混合液1號]";
		mes "* 魔法粉末 *";
		mes " - 8472";
		next;
		mes "[魔法師轉職考試要求的混合液1號]";
		mes "* 催化物 *";
		mes " - 黃色魔力礦石";
		mes "(內藏在物品調和機中)";
		close;
	case 2:
		mes "[魔法師轉職考試要求的混合液2號]";
		mes "* 材料明細 *";
		mes " - 傑勒比結晶 3 個";
		mes " - 毛 1 個";
		mes " - 牛奶 1 個";
		next;
		mes "[魔法師轉職考試要求的混合液2號]";
		mes "* 溶劑 *";
		mes " - 不需要";
		next;
		mes "[魔法師轉職考試要求的混合液2號]";
		mes "* 魔法粉末 *";
		mes " - 3735";
		next;
		mes "[魔法師轉職考試要求的混合液2號]";
		mes "* 催化物 *";
		mes " - 紅色魔力礦石";
		mes "(內藏在物品調和機中)";
		close;
	case 3:
		mes "[魔法師轉職考試要求的混合液3號]";
		mes "* 材料明細 *";
		mes " - 傑勒比結晶 6 個";
		mes " - 毛 1 個";
		next;
		mes "[魔法師轉職考試要求的混合液3號]";
		mes "* 溶劑 *";
		mes "斐揚水溶液";
		mes "所在地：斐揚弓箭手村莊的小泉";
		next;
		mes "[魔法師轉職考試要求的混合液3號]";
		mes "* 魔法粉末 *";
		mes " - 2750";
		next;
		mes "[魔法師轉職考試要求的混合液3號]";
		mes "* 催化物 *";
		mes " - 藍色魔力礦石";
		mes "(內藏在物品調和機中)";
		close;
	case 4:
		mes "[魔法師轉職考試要求的混合液4號]";
		mes "* 材料明細 *";
		mes " - 傑勒比結晶 2 個";
		mes " - 毛 3 個";
		next;
		mes "[魔法師轉職考試要求的混合液4號]";
		mes "* 溶劑 *";
		mes "夢羅克水溶液";
		mes "所在地：夢羅克金字塔入口附近的小泉";
		next;
		mes "[魔法師轉職考試要求的混合液4號]";
		mes "* 魔法粉末 *";
		mes " - 5429";
		next;
		mes "[魔法師轉職考試要求的混合液4號]";
		mes "* 催化物 *";
		mes " - 透明的寶石";
		mes "(內藏在物品調和機中)";
	case 5:
	}
	close;
}