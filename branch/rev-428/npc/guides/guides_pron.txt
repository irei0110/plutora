//==========================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ------------------------------------------
// - 普隆德拉 導遊 腳本 -
// ------------------------------------------
// 版本 1.7
//==========================

prontera,154,187,4	script	導遊#prt::PrtGuide	105,{
	cutin "prt_soldier",2;
	mes "[普隆德拉的導遊]";
	mes "歡迎光臨 非常歡迎您們來到盧恩 米德加爾特王國美麗的首都普隆德拉.";
	mes "我們為了來到普隆德拉的旅客";
	mes "安排了導遊為您們介紹這個城市";
	mes "當您們有問題的時候";
	mes "隨時可以詢問他們";
	mes "第一次來到這裡的旅客　先按選單";
	mes "'選擇注意事項' 了解必須注意的事情.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("開始由導遊說明城市.:刪除微型地圖上全部的記號.:聽取注意事項.: 取消.")) {
		case 1:
			mes "[普隆德拉的導遊]";
			mes "請您選擇想知道的選單.";
			if (.@compass_check == 0) {
				mes "要在微型地圖上留下記號嗎?";
				next;
				if (select("要.:不要.") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("劍士公會:^0000FF教堂^000000:騎士團:武器商店:道具商店: 旅館:工具商店:酒店:圖書館:轉職所:普隆德拉城:辦公室:^FF0000拍賣場^000000:取消")) {
				case 1:
					mes "[普隆德拉的導遊]";
					mes "專門辦理劍士轉職業務的劍士公會已經搬";
					mes "到 新城市依斯魯得";
					mes "普隆德拉現在已經是空屋.";
					if (.@compass_check)
						viewpoint 1,237,41,4,0x00FF00;
					break;
				case 2:
					mes "[普隆德拉的導遊]";
					mes "專門辦理服事轉職業務的教堂位置在普隆德拉東北方的郊區.";
					if (.@compass_check)
						viewpoint 1,236,316,5,0xFF0000;
					break;
				case 3:
					mes "[普隆德拉的導遊]";
					mes "保護盧恩 米德加爾特王國美麗的首都普隆德";
					mes "拉行政機關的騎士團位在普隆德拉西北方的郊區.";
					if (.@compass_check)
						viewpoint 1,46,345,6,0x00FF00;
					break;
				case 4:
					mes "[普隆德拉的導遊]";
					mes "武器商店位在以噴水池中心的東北方.";
					if (.@compass_check)
						viewpoint 1,175,220,7,0xFF00FF;
					break;
				case 5:
					mes "[普隆德拉的導遊]";
					mes "道具商店位在以噴水池中心的西北方.";
					if (.@compass_check)
						viewpoint 1,134,221,8,0xFF00FF;
					break;
				case 6:
					mes "[普隆德拉的導遊]";
					mes "旅館位在以噴水池中心的西方與東方";
					mes "各有一 間.";
					if (.@compass_check) {
						viewpoint 1,204,191,9,0xFF00FF;
						viewpoint 1,107,192,10,0xFF00FF;
					}
					break;
				case 7:
					mes "[普隆德拉的導遊]";
					mes "工具商店位在以噴水池中心的東南方.";
					if (.@compass_check)
						viewpoint 1,179,184,11,0x00FF00;
					break;
				case 8:
					mes "[普隆德拉的導遊]";
					mes "酒店位在以噴水池中心的東南方就在交易所對面.";
					if (.@compass_check)
						viewpoint 1,208,154,12,0x00FF00;
					break;
				case 9:
					mes "[普隆德拉的導遊]";
					mes "圖書館位在從噴水池開始往北方走，看見一塊空地。在空地前的左邊和右邊各有一間.";
					if (.@compass_check) {
						viewpoint 1,120,267,13,0x00FF00;
						viewpoint 1,192,267,14,0x00FF00;
					}
					break;
				case 10:
					mes "[普隆德拉的導遊]";
					mes "轉職所位在以噴水池中心的西南方.";
					if (.@compass_check)
						viewpoint 1,133,183,15,0x00FF00;
					break;
				case 11:
					mes "[普隆德拉的導遊]";
					mes "普隆德拉城堡是位在普隆德拉北方的郊區的12點鐘方向.";
					mes "有一點要注意,";
					mes "城堡的後門是和北普隆德拉相鄰在一起的.";
					if (.@compass_check)
						viewpoint 1,156,360,16,0x00FF00;
					break;
				case 12:
					mes "[普隆德拉的導遊]";
					mes "辦公室位在普隆德拉西南方郊區的";
					mes "7點鐘方向.";
					if (.@compass_check)
						viewpoint 1,75,91,17,0x01FF01;
					break;
				case 13:
					mes "[普隆德拉的導遊]";
					mes "拍賣場是在普隆德拉南東方的郊區的5點鐘方向.";
					if (.@compass_check)
						viewpoint 1,218,120,18,0x00FF00;
				case 14:
					mes "[普隆德拉的導遊]";
					mes "需要刪掉記號請選取";
					mes "'刪掉微型地圖上全部的記號' .";
					set .@loop2,0;
					break;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,4,0x00FF00;
			viewpoint 2,237,41,5,0x0000FF;
			viewpoint 2,46,345,6,0x00FF00;
			viewpoint 2,175,220,7,0xFF0000;
			viewpoint 2,134,221,8,0xFF0000;
			viewpoint 2,204,191,9,0xFF0000;
			viewpoint 2,107,192,10,0xFF0000;
			viewpoint 2,179,184,11,0x00FF00;
			viewpoint 2,208,154,12,0x00FF00;
			viewpoint 2,120,267,13,0x00FF00;
			viewpoint 2,192,267,14,0x00FF00;
			viewpoint 2,133,183,15,0x00FF00;
			viewpoint 2,156,360,16,0x00FF00;
			viewpoint 2,75,91,17,0x00FF00;
			viewpoint 2,218,120,18,0x00FF00;
			set .@compass_check,0;
			break;
		case 3:
			mes "[普隆德拉的導遊]";
			mes "我們廢除了原來較差的指引方式採用最現代化的。無論您在那裡都可以借由微型地圖知道您的位置.";
			mes "您們可以直接確認盧恩米德加爾特王國魔法和科學技術組合後廣域測位系統的功能.";
			next;
			mes "[普隆德拉的導遊]";
			mes "在流覽之前必須先參考右上方的微型地圖.";
			mes "如果看不見微型地圖的話";
			mes "請按'ctrl+tab'或者選取基本情報視窗內的'map'.";
			mes "微型地圖出現之後, 在地圖上按　'-'";
			mes "幾下可以看見所有城市.";
			break;
		case 4:
			mes "[普隆德拉的導遊]";
			mes "祝您旅遊快樂.";
			close2;
			set .@loop1,0;
			break;
		}
	}
	cutin "prt_soldier",255;
	end;
}

prontera,282,208,2	duplicate(PrtGuide)	東門-導遊	105

prontera,29,200,6	duplicate(PrtGuide)	西門導遊	105

prontera,160,29,0	duplicate(PrtGuide)	南門導遊	105

prontera,151,330,4	duplicate(PrtGuide)	北門導遊	105