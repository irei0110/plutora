//===========================================
// [冥] The PlutoEA Project [王]
// http://opensvn.csie.org/PlutoEA
// ------------------------------------------
// - 艾音布羅克 機場 腳本 -
// ------------------------------------------
// 版本 1.3
// ------------------------------------------

airport,126,43,4	script	機場 職員#01::AirportE	90,{
	mes "[機場 職員]";
	mes "您好，我們公司隨時提供給顧客便捷又舒適的旅行服務，會盡其所能的，請問您需要幫忙嗎？";
	next;
	if(select("進入機場內部","取消")==1) {
		mes "[機場 職員]";
		mes "利用飛空艇的費用為1200金幣，如果您持有飛空艇免費搭乘券的話，就能免費進入，請問您要進入機場內部嗎？";
		next;
		if(select("是","否")==1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "airport",148,51;
				close;
			}
			if(zeny < 1200) {
				mes "[機場 職員]";
				mes "你沒有足夠的金錢.";
				close;
			}
			set Zeny,zeny-1200;
			warp "airport",148,51;
			close;
		}
	}
	mes "[機場 職員]";
	mes "請你下次再利用吧，謝謝光臨";
	close;
}

airport,143,43,4	duplicate(AirportE)	機場 職員#02	90
airport,156,43,4	duplicate(AirportE)	機場 職員#03	90

airport,126,51,4	script	機場 職員#04::AirportE2	90,{
	mes "[機場 職員]";
	mes "你好嗎，我們航空隨時提供給顧客便捷又舒適的旅行服務，會盡其所能，請問你需要幫忙嗎?";
	next;
	if(select("離開機場","取消")==1) {
		mes "[機場 職員]";
		mes "如果你要從機場內部離開又想進來的話，要在花一次費用喔，請問您要離開嗎?";
		next;
		if(select("是的","不要")==1) {
			warp "airport",142,40;
			close;
		}
	}
	mes "[機場 職員]";
	mes "請你下次再利用吧，謝謝光臨";
	close;
}

airport,143,51,4	duplicate(AirportE2)	機場 職員#05	90
airport,156,51,4	duplicate(AirportE2)	機場 職員#06	90

einbroch,94,267,4	script	飛行船職員#07	91,{
	mes "[飛行船職員]";
	mes "您好";
	mes "乘坐飛行船";
	mes "能夠使您舒適並快速的";
	mes "到達目的地";
	next;
	mes "[飛行船職員]";
	mes "想坐飛艇的話";
	mes "從這裡的入口請進入";
	mes "想出機場的話，";
	mes "請走那邊的樓梯";
	close;
}
